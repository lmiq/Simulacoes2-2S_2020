# Pergunta:

Estou tentando fazer o potencial de Lennard-Jones ser calculado automaticamente para muitas distâncais, de forma compacta.
No entanto, dá erro. O que tentei fazer foi o seguinte:

```julia
g(x,y,z) = 4*x*[(y/z)^12-(y/z)^6]
x=1;y=2;
z=rand(10)
g(x,y,z)

ERROR: MethodError: convert(::Type{Union{}}, ::LinearAlgebra.Transpose{Float64,Array{Float64,1}}) is ambiguous. Candidates:
```

# Resposta:

Primeiro: vamos mudar o nome das variáveis para o código ficar mais claro: `(eps,sig,r)` em vez de `(x,y,z)`.

Segundo: os colchetes "`[]`" servem para indicar elementos de um vetor, não podem ser usados como parênteses.
Parênteses, colchetes, chaves, são sempre parênteses em qualquer linguagem de programação, então temos:

```julia
g(eps,sig,r) = 4*eps*((sig/r)^12-(sig/r)^6)
```

Esta função recebe três números escalares, eps, sig, r. Então primeiro você testa ela de forma simples,
para o que ela foi implementada:

```julia
julia> eps=1; sig=2; r=5.;

julia> g(eps,sig,r)
-0.016316891136000006

``` 

Agora, o que você quer é, dada uma lista de números, que ela seja calculada para todos os números. Há várias
maneiras de fazer isso, mais ou menos explicitamente (e de forma mais ou menos compacta). Primeiro, vamos
criar um vetor de 3 números aleatórios que vão servir de distâncias `r`, no input da função:

```julia
julia> rvec = rand(3)
```

Agora, vamos aplicar a função a todos os elementos de `rvec`:

1. Forma mais explícita, e menos compacta: crie um vetor para armazenar os resultados antes, e preencha
o vetor com a aplicação da função para cada valor de `rvec`:

```julia
julia> result = zeros(3); # ou resut = Vector{Float64}(undef,3)
julia> for i in 1:3
         result[i] = g(eps,sig,rvec[i])
       end

julia> result
3-element Array{Float64,1}:
 70459.14301818328
     4.9112278714965785e8
     1.5483707586002014e11

```

## Agora, as formas compactas. Importante: nenhuma das formas compactas é muito melhor que a forma acima,
porque todas elas têm que fazer a mesma coisa. É uma questão de notação, simplesmente, e a escolha é uma
escolha de estilo (em Julia, ao contrário de Python, o loop acima é rápido):

2. Usando `map`:

```julia
julia> map(r -> g(eps,sig,r), rvec)
3-element Array{Float64,1}:
 70459.14301818328
     4.9112278714965785e8
     1.5483707586002014e11
```

Note que usamos `r -> g(eps,sig,r)`, que significa: dado `r`, devolta o resultado de `g(eps,sig,r)`, no caso para
todos os elementos de `rvec`.

2. Usando `.`, ou "broadcast", basta colocar um `.` em frente à função e passar o vetor em vez de um escalar
como parâmetro:

```julia
julia> g.(eps,sig,rvec)
3-element Array{Float64,1}:
 70459.14301818328
     4.9112278714965785e8
     1.5483707586002014e11
```

Evidentemente, em ambos os casos, o resultado poderia ser salvo em um vetor (que é criado na saída
da função, não precisa ser criado antes):

```julia
julia> result = map(r -> g(eps,sig,r), rvec)
julia> result = g.(eps,sig,rvec)

```

# Benchmark

Primeiro, vamos colocar a primeira alternativa (aquela que não é compacta), dentro de uma função:

```julia
function gall(eps,sig,rvec)
  result = Vector{Float64}(undef,length(rvec))
  for i in 1:length(rvec)
    result[i] = g(eps,sig,rvec[i])
  end
  return result
end
```

Assim, podemos testar todas e comparar os tempos:

```julia
using BenchmarkTools
using Test
@test 

julia> @test gall(rvec) ≈ g.(eps,sig,rvec) ≈ map(r->g(eps,sig,r),rvec)
Test Passed

julia> @btime gall($eps,$sig,$rvec);
  121.240 ns (1 allocation: 112 bytes)

julia> @btime map(r->g($eps,$sig,r),$rvec);
  122.164 ns (1 allocation: 112 bytes)

julia> @btime g.($eps,$sig,$rvec);
  116.445 ns (1 allocation: 112 bytes)
  
```

Note que todas as opções são eficientes (sendo, neste caso, a opção do broadcast "`.`" ligeiramente
mais rápida apenas.
